def scene_at(now)
{
  var radius = Animations.animate(10, 200, seconds(5))
  var eye = Animations.circular([ "position": pos(0,0,radius[now]),
                                  "around": pos(0,0,0),
                                  "duration": seconds(5) ])

  var camera = Cameras.perspective( [ "eye": eye[now],
                                      "look_at": pos(0,0,0) ] )

  var material = Materials.uniform( [ "ambient": Colors.white() * 0.1,
                                      "diffuse": Colors.white() * 0.8,
                                      "specular": Colors.white(),
                                      "specular_exponent": 10,
                                      "reflectivity": 0.15,
                                      "transparency": 0,
                                      "refractive_index": 0 ] )

  var white = Materials.uniform( [ "ambient": Colors.white() * 0.1,
                                   "diffuse": Colors.white() * 0.8,
                                   "specular": Colors.black(),
                                   "specular_exponent": 0,
                                   "reflectivity": 0,
                                   "transparency": 0,
                                   "refractive_index": 1.5 ] )

  var primitives = []

  // primitives.push_back( decorate( white, translate( vec(0,0,-100), xy_plane() ) ) )

  [-50..50].for_each( bind( fun (i, primitives) {
    var radius = 10
    var angle = degrees(20) * i
    var z = i * 5

    var p = Pos.cylindrical_y(radius, angle, z)
    var q = Pos.cylindrical_y(-radius, angle, z)

    var p_next = Pos.cylindrical_y(radius, angle + degrees(20), z + 5)
    var q_next = Pos.cylindrical_y(-radius, angle + degrees(20), z + 5)

    primitives.push_back( translate( p - pos(0,0,0), sphere() ) )
    primitives.push_back( translate( q - pos(0,0,0), sphere() ) )

    primitives.push_back(rotate_around_y(angle,
                                         translate(vec(0, z, 0),
                                                   scale(1,0.2,0.2,
                                                         crop_along_x( cylinder_along_x(),
                                                                       interval(-radius,radius))))))

    primitives.push_back(translate(p-pos(0,0,0),
                                   align_y_to((p_next-p).normalized(),
                                              scale(0.25,1,0.25,
                                                    crop_along_y(cylinder_along_y(),
                                                                 interval(0, distance(p, p_next)))))))

    primitives.push_back(translate(q-pos(0,0,0),
                                   align_y_to((q_next-q).normalized(),
                                              scale(0.5,1,0.5,
                                                    crop_along_y(cylinder_along_y(),
                                                                 interval(0, distance(q, q_next)))))))
}, _, primitives ) )


  var root = decorate(material, bbunion( primitives ))

  var lights = [ Lights.omnidirectional( pos(-50,50,50), Colors.white() ) ]

  return create_scene(camera, root, lights)
}


var raytracer   = Raytracers.v6()

var renderer    = Renderers.standard( [ "width": 500,
                                        "height": 500,
                                        "sampler": Samplers.multijittered(2),
                                        "ray_tracer": raytracer ] )

pipeline( scene_animation(scene_at, seconds(5)),
          [ Pipeline.animation(30),
            Pipeline.renderer(renderer),
            Pipeline.studio() ] )
