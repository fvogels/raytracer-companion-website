// movie
def mat(c)
{
  return Materials.uniform([ "ambient": c * 0.1,
                             "diffuse": c * 0.8,
                             "specular": Colors.white() * 0.8,
                             "specular_exponent": 10 ])
}

def sph(c)
{
  return decorate( mat(c), sphere() )
}

def scene_at(now)
{
  var t = Animations.animate(0, 1, seconds(5))[now]
  var camera = Cameras.perspective( [ "eye": pos(5,t * 25,0),
                                      "look_at": pos(0,0,0) ] )

  var primitives = []

  for_each([-10..20], bind(fun (i, primitives) {
                     primitives.push_back(translate(vec(-i*3, 0, -2), sph(Colors.red())))
                     primitives.push_back(translate(vec(-i*3, 0, 2), sph(Colors.red())))
                   }, _, primitives))

  var planemat = Materials.uniform([ "ambient": Colors.white() * 0.1,
                                     "diffuse": Colors.white() * 0.8 ])

  primitives.push_back(decorate(planemat, translate(vec(0,0,-5), xy_plane())))
  primitives.push_back(decorate(planemat, translate(vec(0,0,5), xy_plane())))

  var root = union(primitives)

  var lights = [ Lights.omnidirectional( pos(0,-5,0), Colors.white() * 0.5 ), Lights.omnidirectional( pos(0,5,0), Colors.white() * 0.5 ) ]

  create_scene(camera, root, lights)
}

var raytracer   = Raytracers.v4()
var renderer    = Renderers.standard( [ "width": 500,
                                        "height": 500,
                                        "sampler": Samplers.single(),
                                        "ray_tracer": raytracer ] )

pipeline( scene_animation(scene_at, seconds(5)),
          [ Pipeline.animation(30),
            Pipeline.renderer(renderer),
            Pipeline.studio() ] )

