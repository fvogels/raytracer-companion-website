def scene_at(now)
{
  var camera = Cameras.perspective( [ "eye": pos(3,3,3),
                                      "look_at": pos(0,0,0) ] )

  var material = Materials.uniform( [ "ambient": Colors.white() * 0.1,
                                      "diffuse": Colors.white() * 0.8,
                                      "specular": Colors.white(),
                                      "specular_exponent": 10,
                                      "reflectivity": 0,
                                      "transparency": 0,
                                      "refractive_index": 0 ] )

  var offset = Animations.animate(degrees(0), degrees(90), seconds(1))[now]
  var triangles = []

  var SUBDIVISIONS = 50
  [0..SUBDIVISIONS-1].for_each( bind( fun (i, triangles, subdivisions, offset) {
    
    var RADIUS = 1
    var HEIGHT = 2

    var delta = degrees(360) / subdivisions
    var angle = delta * i

    var p1 = Pos.cylindrical_x(RADIUS, angle, 0)
    var p2 = Pos.cylindrical_x(RADIUS, angle + delta, 0)
    var p3 = Pos.cylindrical_x(RADIUS, angle + offset, HEIGHT)
    var p4 = Pos.cylindrical_x(RADIUS, angle + offset + delta, HEIGHT)

    triangles.push_back( triangle(p1, p2, p3) )
    triangles.push_back( triangle(p1, p3, p2) )
    triangles.push_back( triangle(p2, p3, p4) )
    triangles.push_back( triangle(p2, p4, p3) )
  }, _, triangles, SUBDIVISIONS, offset ) )

  var root = decorate(material, center(pos(0,0,0), bbunion( triangles )))

  var lights = [ Lights.omnidirectional( pos(5,5,5), Colors.white() ) ]

  return create_scene(camera, root, lights)
}


var raytracer   = Raytracers.v6()

var renderer    = Renderers.standard( [ "width": 500,
                                       "height": 500,
                                       "sampler": Samplers.multijittered(2),
                                       "ray_tracer": raytracer ] )

pipeline( scene_animation(scene_at, seconds(1)),
          [ Pipeline.animation(30),
            Pipeline.renderer(renderer),
            Pipeline.wif("demo.wif") ] )
